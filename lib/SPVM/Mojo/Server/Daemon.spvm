# Copyright (c) 2025 Yuki Kimoto
# MIT License

class Mojo::Server::Daemon extends Mojo::Server {
  version_from Mojolicious;
  
  use Mojo::Transaction::WebSocket;
  use Mojo::URL;
  use Mojo::Util;
  use Mojo::WebSocket;
  use IO::Socket;
  use IO::Socket::IP;
  use Mojo::Connection::Server;
  use Mojo::Connection::Stream;
  use Go::Channel;
  use Go::OS::Signal;
  use Sys::Signal::Constant as SIGNAL;
  use Go::Sync::WaitGroup;
  use Sys::Socket::Constant as SOCKET;
  
  # Fields
  has acceptors : rw Mojo::Connection::Server[];
  
  has backlog : rw int;
  
  has max_clients : rw int;
  
  has inactivity_timeout : rw int;
  
  has keep_alive_timeout : rw int;
  
  has max_requests : rw int;
  
  has listen : rw string[];
  
  has reverse_proxy : rw byte;
  
  has trusted_proxies : rw string[];
  
  has silent : rw byte;
  
  has stop_signal_handler_ch : Go::Channel;
  
  # Class Methods
  static method new : Mojo::Server::Daemon () {
    
    my $self = new Mojo::Server::Daemon;
    
    $self->init;
    
    return $self;
  }
  
  static method DEBUG : int () {
    return length(my $_ = Sys->env("SPVM_MOJO_SERVER_DEBUG")) ? (int)$_ : 0;
  }
  
  # Instance Methods
  protected method init : void () {
    
    $self->{acceptors} = new Mojo::Connection::Server[0];
    
    $self->{inactivity_timeout} = length(my $_ = Sys->env("SPVM_MOJO_INACTIVITY_TIMEOUT")) ? (int)$_ : 30;
    
    $self->{keep_alive_timeout} = length(my $_ = Sys->env("SPVM_MOJO_KEEP_ALIVE_TIMEOUT")) ? (int)$_ : 5;
    
    $self->{max_requests} = 100;
    
    $self->{listen} = length(my $_ = Sys->env("SPVM_MOJO_KEEP_ALIVE_TIMEOUT")) ? Fn->split(",", $_) : ["http://*:3000"];
  }
  
  method ports : int[] () {
    
    my $acceptors = $self->acceptors;
    
    my $ports_list = IntList->new;
    for (my $i = 0; $i < @$acceptors; $i++) {
      my $handle = $acceptors->[$i];
      $ports_list->push($handle->port);
    }
    
    return $ports_list->to_array;
  }
  
  method run : void () {
    
    my $stop_signal_handler_ch = Go->make(1);
    
    Go->go([$self : Mojo::Server::Daemon, $stop_signal_handler_ch : Go::Channel] method : void () {
      
      my $signal_ch = Go->make(1);
      Go::OS::Signal->notify($signal_ch, SIGNAL->SIGTERM);
      Go::OS::Signal->notify($signal_ch, SIGNAL->SIGINT);
      
      my $select = Go->new_select;
      $select->add_read($signal_ch);
      $select->add_read($stop_signal_handler_ch);
      
      while (1) {
        
        my $result = $select->select;
        
        if ($result) {
          if ($result->channel == $signal_ch) {
            $self->stop;
          }
          elsif ($result->channel == $stop_signal_handler_ch) {
            last;
          }
        }
      }
    });
    
    $self->{stop_signal_handler_ch} = $stop_signal_handler_ch;
    
    $self->start;
  }
  
  method start : void () {
    
    my $max = $self->max_clients;
    
    # Start listening
    if (!@{$self->acceptors}) {
      $self->app->server($self);
      for my $_ (@{$self->listen}) {
        $self->_listen($_);
      }
    }
    
    # Start accept
    my $wg_listens = Go::Sync::WaitGroup->new;
    $wg_listens->add(@{$self->acceptors});
        
    my $max_clients = $self->max_clients;
    
    my $semaphore_accept = Go->make($max_clients);
    
    for my $acceptor (@{$self->acceptors}) {
      
      Go->go([$self : Mojo::Server::Daemon, $acceptor : Mojo::Connection::Server, $wg_listens : Go::Sync::WaitGroup, $semaphore_accept : Go::Channel] method : void () {
        
        Fn->defer([$wg_listens : Go::Sync::WaitGroup] method : void () {
          $wg_listens->done;
        });
        
        while (1) {
          $semaphore_accept->write(1);
          
          my $accepted_handle = (IO::Socket)undef;
          eval { $accepted_handle = $acceptor->handle->accept; }
          
          if ($@) {
            my $ok = 0;
            $semaphore_accept->read(\$ok);
            next;
          }
          
          Go->go([$self : Mojo::Server::Daemon, $semaphore_accept : Go::Channel, $accepted_handle : IO::Socket] method : void () {
            Fn->defer([$semaphore_accept : Go::Channel] method : void () {
              my $ok = 0;
              $semaphore_accept->read(\$ok);
            });
            
            # Disable Nagle's algorithm
            $accepted_handle->setsockopt(SOCKET->IPPROTO_TCP, SOCKET->TCP_NODELAY, 1);
            
            my $accepted_connection = Mojo::Connection::Stream->new;
            $accepted_connection->set_handle($accepted_handle);
            
            my $tx = $self->_build_tx($accepted_connection);
            $accepted_connection->set_tx($tx);
            
            my $chunk = new_string_len 4096;
            my $chunk_length = length $chunk;
            while (1) {
              if ($tx->req->is_finished) {
                last;
              }
              
              my $read_length = $accepted_connection->read($chunk);
              
              $tx->server_read($read_length < $chunk_length ? Fn->substr($chunk, 0, $read_length) : (string)$chunk);
            }
            
            # Do process
            my $req = $tx->req;
            
            # WebSocket
            if ($req->is_handshake) {
              
              my $ws = Mojo::Transaction::WebSocket->new;
              $ws->set_handshake($tx);
              
              $accepted_connection->set_next($ws);
              
              $self->app->handler(Mojo::WebSocket->server_handshake($ws));
            }
            
            # HTTP
            else {
              $self->app->handler($tx);
            }
            
            # Last keep-alive request or corrupted connection
            if (($accepted_connection->requests || 1) >= $self->max_requests) {
              $tx->res->headers->set_connection("close");
            }
            
            my $output = "";
            while (1) {
              my $chunk = $tx->server_write;
              
              unless (length $chunk) {
                last;
              }
              
              $accepted_connection->write($chunk);
            }
            
          });
        }
      });
    }
    
    $wg_listens->wait;
  }
  
  method stop : void () {
    
    for my $server (@{$self->acceptors}) {
      $server->stop;
    }
    
  }
  
  method _build_tx : Mojo::Transaction::HTTP ($accepted_connection : Mojo::Connection::Stream) {
    
    my $tx = $self->build_tx;
    
    $tx->set_connection($accepted_connection);
    
    $tx->res->headers->set_server("Mojolicious (SPVM)");
    
    my $handle = $accepted_connection->handle;
    
    # TODO: set this to goroutine
    $accepted_connection->set_timeout($self->inactivity_timeout);
    
    unless ($handle isa IO::Socket::UNIX) {
      $tx->set_local_address($handle->(IO::Socket::IP)->sockhost);
      $tx->set_local_port($handle->(IO::Socket::IP)->sockport);
      $tx->set_remote_address($handle->(IO::Socket::IP)->peerhost);
      $tx->set_remote_port($handle->(IO::Socket::IP)->peerport);
    }
    
    if ($accepted_connection->tls) {
      $tx->req->url->base->set_scheme("https");
    }
    
    # Kept alive if we have more than one request on the connection
    my $requests = $accepted_connection->requests;
    ++$requests;
    
    if ($requests > 1) {
      $tx->set_kept_alive(1);
    }
    
    $accepted_connection->set_requests($requests);
    
    return $tx;
  }
  
  method _finish : void ($c : Mojo::Connection::Stream) {
    
    # Always remove connection for WebSockets
    my $tx = $c->tx;
    unless ($tx) {
      return;
    }
    
    $tx->closed;
    
    my $ws = (Mojo::Transaction::WebSocket)$c->next;
    $c->set_next(undef);
    
    # Upgrade connection to WebSocket
    if ($ws) {
      # Successful upgrade
      if ($ws->handshake->res->code == 101) {
        $ws->set_established(1);
        $c->set_tx($ws);
        
        # TODO : write logic
      }
      
      # Failed upgrade
      else { $ws->closed; }
    }
    
    # Close connection if necessary
    if ($tx isa Mojo::Transaction::HTTP && !$tx->(Mojo::Transaction::HTTP)->keep_alive) {
      $tx->closed;
    }
    
    # Build new transaction for leftovers
    if (length(my $leftovers = $tx->req->content->leftovers)) {
    
      $tx = $self->_build_tx($c);
      
      $c->set_tx($tx);
      
      $tx->server_read($leftovers);
    }
    
    # Keep-alive connection
    unless ($c->tx) {
      $c->set_timeout($self->keep_alive_timeout);
    }
  }
  
  private static method _peer : string ($handle : IO::Socket) {
    
    my $peer = (string)undef;
    
    if ($handle isa IO::Socket::UNIX) {
      $peer = $handle->(IO::Socket::UNIX)->peerpath;
    }
    else {
      $peer = $handle->(IO::Socket::IP)->peerhost;
    }
    
    return $peer;
  }
  
  private static method _url : string ($tx : Mojo::Transaction) {
    
    return $tx->req->url->to_abs->to_string;
  }
  
  method _listen : void ($listen : string) {
    
    my $url = Mojo::URL->new($listen);
    
    my $proto = $url->protocol;
    unless ($proto eq "http" || $proto eq "https" || $proto eq "http+unix") {
      die "Invalid listen location '$listen'";
    }
    
    my $query   = $url->query;
    my $options = Hash->new({backlog => $self->backlog});
    
    $options->set("fd" => $query->param("fd"));
    $options->set("reuse" => $query->param("reuse"));
    
    if ($proto eq "http+unix") { $options->set(path => $url->host); }
    else {
      if ((my $host = $url->host) ne "*") { $options->set(address => $host); }
      if (my $port = $url->port) { $options->set(port => $port); }
    }
    
    $options->set(tls_ca => $query->param("ca"));
    
    for my $_ (@{$query->names}) {
      if (my $m = Re->m($_, "^(.*)_(cert|key)$")) {
        my $type = $m->cap1;
        my $name = "tls_" . $m->cap2;
        
        $options->set($name, Hash->new);
        $options->get($name)->(Hash)->set($type, $query->param($_));
      }
    }
    
    if (my $cert = $query->param("cert")) { $options->get("tls_cert")->(Hash)->set("", $cert); }
    if (my $key  = $query->param("key"))  { $options->get("tls_key")->(Hash)->set("", $key); }
    my $ciphers = $query->param("ciphers");
    my $verify = $query->param("verify");
    my $version = $query->param("version");
    
    $options->set(tls_options => Hash->new);
    
    if ($ciphers) {
      $options->get("tls_options")->(Hash)->set(SSL_cipher_list => $ciphers);
    }
    if ($verify) {
      $options->get("tls_options")->(Hash)->set(SSL_verify_mode => Fn->hex($verify));
    }
    if ($version) {
      $options->get("tls_options")->(Hash)->set(SSL_version => $version);
    }
    
    my $tls =  $proto eq "https";
    $options->set(tls => $tls);
    
    my $acceptor = Mojo::Connection::Server->new;
    
    $acceptor->set_timeout($self->inactivity_timeout);
    
    $acceptor->listen($options);
    
    my $acceptors = (List of Mojo::Connection::Server)List->new_ref($self->{acceptors});
    
    $acceptors->push($acceptor);
    
    if ($self->silent) {
      return;
    }
    
    $self->app->log->info("Listening at '" . $url->to_string . "'");
    $query->set_pairs(new string[0]);
    
    if ($url->host eq "*") {
      $url->set_host("127.0.0.1");
    }
    if (!$options->get("path") && !$url->port) {
      $url->set_port($self->ports->[@$self->ports - 1]) ;
    }
    
    say "Web application available at " . ($options->{"path"}->(string) // $url->to_string);
  }

}
