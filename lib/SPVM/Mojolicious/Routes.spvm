# Copyright (c) 2025 Yuki Kimoto
# MIT License

class Mojolicious::Routes extends Mojolicious::Routes::Route {
  version_from Mojolicious;
  
  use Hash;
  use Mojo::Cache;
  use Mojolicious::Callback;
  use Mojolicious::Controller;
  use Mojolicious::Routes::Match::StackData;
  
  # Fields
  has cache : rw Mojo::Cache;
  
  # Class Methods
  static method new : Mojolicious::Routes () {
    
    my $self = new Mojolicious::Routes;
    
    $self->init;
    
    return $self;
  }
  
  # Instance Methods
  protected method init : void ($options : object[] = undef) {
    
    $self->SUPER::init;
    
    $self->{cache} = Mojo::Cache->new;
  }
  
  method dispatch : int ($c : Mojolicious::Controller) {
    
    $self->match($c);
    
    unless (@{$c->match->stack}) {
      return 0;
    }
    
    $self->continue($c);
    
    unless ($c->rendered) {
      die "Not rendered.";
    }
    
    return 1;
  }
  
  method match : void ($c : Mojolicious::Controller) {
    
    my $req  = $c->req;
    my $path = $req->url->path->to_route;
    
    # Method (HEAD will be treated as GET)
    my $method = Fn->uc($req->method);
    if ($method eq "HEAD") {
      $method = "GET";
    }
    
    # Check cache
    my $ws = $c->tx->is_websocket ? 1 : 0;
    my $match = $c->match;
    $match->set_root($self);
    my $cache = $self->cache;
    if (my $result = $cache->get("$method:$path:$ws")) {
      $match->set_endpoint($result->{"endpoint"}->(Mojolicious::Routes::Route));
      $match->set_stack($result->{"stack"}->(Mojolicious::Routes::Match::StackData[]));
      return;
    }
    
    # Check routes
    $match->find(Hash->new({method => $method, path => $path, websocket => $ws}));
    
    my $endpoint = $match->endpoint;
    
    unless ($endpoint) {
      return;
    }
    
    $cache->set("$method:$path:$ws" => Hash->new({endpoint => $endpoint, stack => $match->stack}));
  }
  
  method continue : int ($c : Mojolicious::Controller) {
    
    my $match = $c->match;
    my $stack = $match->stack;
    my $position = $match->position;
    
    unless ($position < @$stack) {
      die "[Unexpected Error]Invalid stack position.";
    }
    
    my $stack_data = $stack->[$position];
    my $captures = $stack_data->captures;
    
    $c->set_captures($captures);
    
    my $r = $stack_data->route;
    
    my $continue = 1;
    if (my $cb = $r->cb) {
      $c->log->trace("Routing to a callback");
      
      $continue = $cb->($c);
    }
    elsif (my $template = $r->template) {
      
      my $format = $r->format;
      
      my $handler = $r->handler;
      
      $c->log->trace("Routing to the template $template.$format.$handler");
      
      my $output = $c->render($template, {format => $format, handler => $handler});
      
      if ($output) {
        $c->set_rendered(1);
      }
      
      $continue = 0;
    }
    
    my $last = ($position == @$stack - 1);
    if ($last) {
      $continue = 0;
    }
    
    $match->set_position($position + 1);
    
    if ($continue) {
      return $self->continue($c);
    }
    else {
      unless ($c->rendered) {
        die "Not rendered.";
      }
    }
  }
  
}
