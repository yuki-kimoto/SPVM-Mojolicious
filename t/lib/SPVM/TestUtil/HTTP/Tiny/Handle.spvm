# Copyright (c) 2023 Yuki Kimoto
# MIT License

class TestUtil::HTTP::Tiny::Handle {
  version_from Mojo;
  
  allow TestUtil::HTTP::Tiny;
  
  use Mojo::Message::Request;
  use Mojo::Message::Response;
  
  use Fn;
  use Re;
  use IO::Socket::INET;
  
  # Enumerations
  private enum {
    READ_BUFFER_SIZE = 4086,
  }
  
  # Fields
  has timeout : double;
  
  has socket : IO::Socket;
  
  private static method new : TestUtil::HTTP::Tiny::Handle ($options : object[]) {
    
    my $options_h = Hash->new($options);
    
    my $self = new TestUtil::HTTP::Tiny::Handle;
    
    # The timeout field
    my $timeout = $options_h->delete_or_default_double("timeout", 60.0);
    $self->{timeout} = $timeout;
    
    return $self;
  }
  
  private method connect : TestUtil::HTTP::Tiny::Handle ($req : Mojo::Message::Request) {
    
    my $url = $req->url;
    my $scheme = $url->scheme;
    my $host = $url->host;
    my $port = $url->port;
    
    my $timeout = $self->{timeout};
    
    my $socket = (IO::Socket)undef;
    
    if ($scheme eq "http") {
      $socket = IO::Socket::INET->new({
        PeerAddr  => $host,
        PeerPort  => $port,
        Timeout   => $timeout,
      });
    }
    elsif ($scheme eq "https") {
      if (require IO::Socket::SSL) {
        $socket = IO::Socket::SSL->new({
          PeerAddr  => $host,
          PeerPort  => $port,
          Timeout   => $timeout,
        });
      }
      else {
        die "IO::Socket::SSL is required for HTTPS access.";
      }
    }
    else {
      die "The scheme $scheme is not supported.";
    }
    
    $self->{socket} = $socket;
    
    return $self;
  }
  
  private method write_request : void ($req : Mojo::Message::Request) {
    
    my $socket = $self->{socket};
    
    my $start_line = $req->build_start_line;
    
    $socket->write($start_line);
    
    my $header_lines = $req->build_headers;
    
    $socket->write($header_lines);
    
    $socket->write("\r\n");
  }
  
  private method read_response : Mojo::Message::Response () {
    
    my $res = Mojo::Message::Response->new;
    
    my $handle = $self->{socket};
    
    while (1) {
      my $chunk = (mutable string)new_string_len &READ_BUFFER_SIZE;
      
      my $read_length = $handle->read($chunk);
      
      if ($read_length == 0) {
        $res->finish;
        last;
      }
      
      Fn->shorten($chunk, $read_length);
      
      $res->parse($chunk);
      
      if ($res->is_finished) {
        last;
      }
    }
    
    return $res;
  }
  
  method _find_CA_file : int () {
    return 0;
  }
}
